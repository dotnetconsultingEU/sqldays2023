-- Disclaimer
-- Dieser Quellcode ist als Vorlage oder als Ideengeber gedacht. Er kann frei und ohne 
-- Auflagen oder Einschränkungen verwendet oder verändert werden.
-- Jedoch wird keine Garantie übernommen, dass eine Funktionsfähigkeit mit aktuellen und 
-- zukünftigen API-Versionen besteht. Der Autor übernimmt daher keine direkte oder indirekte 
-- Verantwortung, wenn dieser Code gar nicht oder nur fehlerhaft ausgeführt wird.
-- Für Anregungen und Fragen stehe ich jedoch gerne zur Verfügung.

-- Thorsten Kansy, www.dotnetconsulting.eu

-- Datenbank anlegen und wechseln
CREATE DATABASE dotnetconsulting_AlwaysEncrypted;
GO
USE dotnetconsulting_AlwaysEncrypted;
GO

-- Column Master Key anlegen
CREATE COLUMN MASTER KEY [MasterColumnKey]
WITH
(
	KEY_STORE_PROVIDER_NAME = N'MSSQL_CERTIFICATE_STORE',
	KEY_PATH = N'CurrentUser/My/7A5C4EA066F00A92B38F31DAF5343FC3928373C0'
)
GO

-- Column EntryptionKey anlegen, dabei den Column Master Key verwenden
CREATE COLUMN ENCRYPTION KEY [ColumnEncryptionKey]
WITH VALUES
(
	COLUMN_MASTER_KEY = [MasterColumnKey],
	ALGORITHM = 'RSA_OAEP',
	ENCRYPTED_VALUE = 0x016E000001630075007200720065006E00740075007300650072002F006D0079002F0037006100350063003400650061003000360036006600300030006100390032006200330038006600330031006400610066003500330034003300660063003300390032003800330037003300630030003292ED8398794F04E16D7BCF1E5BA9FF650F52D02F54B387FFCE6E651A18F6B6B8B601444503D90B1318CE6D1E3FF3F03054C97C002EA48CD1ACCA455C4051F2AB054079E9D934E014DE6303F0420C08DEAB52631010109F8C2192E779BA21C45B950185FFA5A60B6E9A2EA3125D173AEA63C4DBBFA9F2DDE6D5F4DF01BBF28416FF7DC23BE715F2D3C27F2D7C7424F95CB3A652BF331553206D81B4A91B8978BADCDAA4DBB6727C0EB19C90EEB1487F43B83AC67896558CF5ABE5AD5B787B9C30C4DFBBD0B7788EE4DFC8C14D1C7B7CA110382AA8FCCB49FC07ED59533E4851C947AE21AACFE8BAD01A6C0D7C64757FCB4245B74DFCBAB1EA19F630379D3A2C0A1AF05FA6E8D9284AC05AB1A74A0B3BE7593EC271C599636231FF9741C07835EFF25E6DAD60B155F865893AA46E0CEEE700335AFDC2FF8D23B477597859615AE0CFA0C6D43BF4A65DB5054B9BA1AE66EAE2335887A982CAE8D34DB1DC2084AA4484277DDD6F4D9B49EA52560E7AD831CE1BE022B4FD994C85460F8A27A1443A009952130C0C22F42A9ABCA40DE12FFD60CB38174C5F400E05ED7CAF1309015C1C4D7A60D6DCA5A3505EDA05F8DD7710093956BDA7BF29D515387E040C805E23EF78532C736BC5144D62394B049034D952442E72381B5A111926E49387EBE2E36C243E3D90051CCDCD52EC2C47B73BD1590B2A5EA5F5699BA5303FAAD0679839
)
GO

-- Tabelle mit zwei verschlüsselten Spalten anlegen
CREATE TABLE [dbo].[Secrets](
    [ID] INT IDENTITY PRIMARY KEY, 

    [User] NVARCHAR(50) COLLATE Latin1_General_BIN2 
      ENCRYPTED WITH (ENCRYPTION_TYPE = DETERMINISTIC, 
      ALGORITHM = 'AEAD_AES_256_CBC_HMAC_SHA_256', 
      COLUMN_ENCRYPTION_KEY = ColumnEncryptionKey) NOT NULL,

    [Secret] NVARCHAR(50) 
      ENCRYPTED WITH (ENCRYPTION_TYPE = RANDOMIZED, 
      ALGORITHM = 'AEAD_AES_256_CBC_HMAC_SHA_256', 
      COLUMN_ENCRYPTION_KEY = ColumnEncryptionKey) NOT NULL,

    [Unwichtig] NVARCHAR(100) NULL 
);
 GO

-- Und Daten einfügen?
INSERT [dbo].[Secrets] ([User], [Secret]) 
VALUES 
(N'James Bond', N'...jagt Dr. No');

-- So leider nicht ;-(
DECLARE @user NVARCHAR(50) = 'James Bond';
DECLARE @secret NVARCHAR(50) = '...jagt Dr. No';
INSERT [dbo].[Secrets] ([User], [Secret]) VALUES (@user, @secret);


-- Connection String für ADO.NET 4.6
-- Data Source=.; Initial Catalog=dotnetconsulting_AlwaysEncrypted; Integrated Security=true; Column Encryption Setting=enabled;